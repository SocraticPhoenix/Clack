//Plugins
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.sf.gapt:apt:1"
    }
}


plugins {
    id 'com.jfrog.bintray' version '1.3.1'
}

apply plugin : 'java'
apply plugin : 'maven'
apply plugin : 'maven-publish'
apply plugin : 'idea'
apply plugin : 'net.sf.gapt'
//End Plugins

//Program Specific Variables (Fill these in)
def programVersion = "0.0.3"
def programGroup = "com.gmail.socraticphoenix"
def programName = "Clack"
def repoName = "Main"
def repoPackageName = "java-apps"
def programLicense = "MIT"
def versionControlURL = "https://github.com/SocraticPhoenix/Clack.git"
//End Variables

//Gradle Variables
group = "$programGroup"
version = "$programVersion"
sourceCompatibility = 1.8
//End Gradle Variables

//Shadow Configuration
configurations {
    shadow
    compile.extendsFrom shadow
}
//End Shadow Configuration

//Repositories and Dependencies (Fill these in)
repositories {
    mavenCentral()
    maven {
        name 'My Repo'
        url 'https://dl.bintray.com/meguy26/Main'
    }
}

dependencies {
    shadow "com.gmail.socraticphoenix:NebulaAPI:0.0.10"
    shadow "com.gmail.socraticphoenix:JEncoding:0.0.1"
    shadow "com.intellij:forms_rt:7.0.3"
}
//End Repositories and Dependencies

//Tasks
task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc){
    classifier = "javadoc"
    from javadoc.destinationDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

javadoc.options.encoding = 'UTF-8'
//End Tasks

//Jar Configuration
jar {
    from configurations.shadow.collect {(it.isDirectory() ? fileTree(it) : zipTree(it)).matching {}}
}
//End Jar Configuration

//BinTray Publishing
publishing {
    publications {
        maven (MavenPublication) {
            artifact jar {
                classifier ""
            }

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            groupId "$programGroup"
            artifactId "$programName"
            version "$programVersion"
        }
    }

}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ["maven"]
    publish = true
    pkg {
        repo = "$repoName"
        name = "$repoPackageName"
        licenses = ["$programLicense"]
        vcsUrl = "$versionControlURL"

        version {
            name = "$programVersion"
        }
    }
}
//End Bintray Publishing
